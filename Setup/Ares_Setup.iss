; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Ares"
#define MyAppVersion "3.3.0"
#define MyAppPublisher "Joerg Ruedenauer"
#define MyAppURL "http://aresrpg.sourceforge.net/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1BF62B6B-F82D-4337-87CB-E8B246F8024D}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputDir=..\build\output
OutputBaseFilename=Ares-{#MyAppVersion}-Setup
Compression=lzma2
SolidCompression=yes
UninstallDisplayIcon=..\build\Ares\Player_Editor\Ares.ico
UninstallDisplayName={#MyAppName}
ChangesAssociations=yes
SignTool=signscript
SignedUninstaller=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; Components: Editor Controller Player

[Files]
Source: "..\build\Ares\Controller\*"; DestDir: "{app}\Controller\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: Controller
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "..\build\Ares\ChangeLog.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Controller Editor Player
Source: "..\build\Ares\ChangeLog_de.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Controller Editor Player
Source: "..\build\Ares\LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Controller Editor Player
Source: "..\build\Ares\lizenz-deutsch.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Controller Editor Player
Source: "..\build\Ares\Player_Editor\Views\*"; DestDir: "{app}\Player_Editor\Views\"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: Player
Source: "..\build\Ares\Player_Editor\Images\*"; DestDir: "{app}\Player_Editor\Images\"; Flags: ignoreversion createallsubdirs recursesubdirs; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.CmdLinePlayer.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.CmdLinePlayer.exe.config"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.Player.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.Player.exe.config"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.WinSecurity.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\MouseKeyboardActivityMonitor.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\MouseKeyboardHooksLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\de\Ares.CmdLinePlayer.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\de\Ares.Player.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\oggenc2.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\opusenc.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.Client.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.Common.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.Interfaces.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.OrmLite.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\ServiceStack.Razor.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\System.Web.Razor.Unofficial.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Player
Source: "..\build\Ares\Player_Editor\Ares.Editor.exe"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Editor.Actions.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Editor.Controls.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Editor.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\de\Ares.TagsImport.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\plugins\Ares.AudioSource.Freesound.dll"; DestDir: "{app}\Player_Editor\plugins\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\plugins\Ares.AudioSource.Freesound.dll.config"; DestDir: "{app}\Player_Editor\plugins\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\plugins\de\Ares.AudioSource.Freesound.resources.dll"; DestDir: "{app}\Player_Editor\plugins\de\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.Editor.Actions.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.Editor.Controls.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.Editor.AudioSource.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.Editor.exe.config"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.TagsImport.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\Ares.TagsImport.dll.config"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\avcodec-54.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\avutil-52.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\chromaprint.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\ChromaprintLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\FfmpegLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\msvcp100.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\msvcr100.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\RestSharp.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\RestSharpLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\TipsOfTheDay.xml"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\TipsOfTheDay_de.xml"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\WeifenLuo.WinFormsUI.Docking.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\WeifenLuo.WinFormsUI.License.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor
Source: "..\build\Ares\Player_Editor\de\Ares.CommonGUI.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Data.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.ModelInfo.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Online.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Players.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Playing.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\de\Ares.Settings.resources.dll"; DestDir: "{app}\Player_Editor\de\"; Flags: ignoreversion; Components: Player Editor
Source: "..\build\Ares\Player_Editor\Ares.CommonGUI.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Data.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.ico"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Ipc.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.ModelInfo.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Online.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Players.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Players.dll.config"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Playing.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Settings.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Ares.Tags.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bass.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\Bass.Net.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bass_aac.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bass_fx.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bassenc.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bassflac.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bassmix.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\bassopus.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\BassRegistration.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\basswma.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\HtmlRenderer.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\HtmlRendererLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\ICSharpCode.SharpZipLib.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\ServiceStack.Text.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\ServiceStackLicence.txt"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\SQLite.Interop.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\sqlite3.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player
Source: "..\build\Ares\Player_Editor\System.Data.SQLite.dll"; DestDir: "{app}\Player_Editor\"; Flags: ignoreversion; Components: Editor Player

[Icons]
Name: "{group}\{#MyAppName} Editor"; Filename: "{app}\Player_Editor\{#MyAppName}.Editor.exe"; Components: Editor
Name: "{commondesktop}\{#MyAppName} Editor"; Filename: "{app}\Player_Editor\{#MyAppName}.Editor.exe"; Components: Editor; Tasks: desktopicon
Name: "{group}\{#MyAppName} Player"; Filename: "{app}\Player_Editor\{#MyAppName}.Player.exe"; Components: Player
Name: "{commondesktop}\{#MyAppName} Player"; Filename: "{app}\Player_Editor\{#MyAppName}.Player.exe"; Components: Player; Tasks: desktopicon
Name: "{group}\{#MyAppName} Controller"; Filename: "{app}\Controller\{#MyAppName}.Controller.exe"; Components: Controller
Name: "{commondesktop}\{#MyAppName} Controller"; Filename: "{app}\Controller\{#MyAppName}.Controller.exe"; Components: Controller; Tasks: desktopicon

[Run]
Filename: "{app}\Player_Editor\Ares.WinSecurity.exe"; Parameters: "AddPort"; WorkingDir: "{app}\Player_Editor"; Flags: postinstall runascurrentuser; Description: "{cm:openFirewall}"; Components: Player
Filename: "{app}\Player_Editor\Ares.Editor.exe"; Flags: nowait postinstall skipifsilent skipifdoesntexist; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Components: Editor; Check: ShallStartEditor
Filename: "{app}\Player_Editor\Ares.Player.exe"; Flags: nowait postinstall skipifsilent skipifdoesntexist; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Components: Player; Check: ShallStartPlayer
Filename: "{app}\Controller\Ares.Controller.exe"; Flags: nowait postinstall skipifsilent skipifdoesntexist; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Components: Controller; Check: ShallStartController

[Components]
Name: "Player"; Description: "{cm:compPlayerDesc}"; Types: All Player_and_Editor Player_only
Name: "Editor"; Description: "{cm:compEditorDesc}"; Types: Player_and_Editor All
Name: "Controller"; Description: "{cm:compControllerDesc}"; Types: Controller_only All

[Types]
Name: "All"; Description: "{cm:typeAllDesc}"
Name: "Player_and_Editor"; Description: "{cm:typePEDesc}"
Name: "Player_only"; Description: "{cm:typePlayerDesc}"
Name: "Controller_only"; Description: "{cm:typeControllerDesc}"

[Dirs]
Name: "{app}\Player_Editor\de\"
Name: "{app}\Player_Editor\Images\"
Name: "{app}\Player_Editor\Views\"
Name: "{app}\Player_Editor\Views\images\"
Name: "{app}\Player_Editor\Views\images\icons-png\"
Name: "{app}\Player_Editor\Views\images\icons-svg\"

[CustomMessages]
english.typeAllDesc=Full (Editor, Player and Controller)
german.typeAllDesc=Alles (Editor, Player und Controller)
english.typePEDesc=Editor and Player
german.typePEDesc=Editor und Player
english.typePlayerDesc=Player only
german.typePlayerDesc=Nur Player
english.typeControllerDesc=Controller only
german.typeControllerDesc=Nur Controller
english.compEditorDesc=Editor
german.compEditorDesc=Editor
english.compPlayerDesc=Player
german.compPlayerDesc=Player
english.compControllerDesc=Controller
german.compControllerDesc=Controller
english.needsNet45=The Ares Editor needs the .NET Framework 4.5 (full) or higher.%n%nPlease download and install it and then re-run the Ares setup program.
german.needsNet45=Der Ares Editor benötigt das .NET Framework 4.5 (full) oder höher.%n%nBitte installieren Sie das .NET Framework und starten Sie das Ares Setup dann erneut.
english.needsNet4=The Ares Player needs the .NET Framework 4 (full) or higher.%n%nPlease download and install it and then re-run the Ares setup program.
german.needsNet4=Der Ares Player benötigt das .NET Framework 4 (full) oder höher.%n%nBitte installieren Sie das .NET Framework und starten Sie das Ares Setup dann erneut.
english.needsJava=The Ares Controller needs Java 6 or higher.%n%nPlease download and install it from http://www.java.com/getjava/ and then re-run the Ares setup program.
german.needsJava=Der Ares Controller benötigt Java 6 oder höher.%n%nBitte installieren Sie Java von http://www.java.com/getjava/ und starten Sie das Ares Setup dann erneut.
english.openFirewall=Configure firewall to allow Web Controller access
german.openFirewall=Firewall konfigurieren für Web Controller Zugriff

[Registry]
Root: HKLM; Subkey: "Software\Ares"; Flags: uninsdeletekeyifempty
Root: "HKLM"; Subkey: "Software\Ares"; ValueType: string; ValueName: "PlayerPath"; ValueData: "{app}\Player_Editor\Ares.Player.exe"; Components: Player
Root: HKCR; Subkey: ".ares"; ValueType: string; ValueName: ""; ValueData: "AresProject"; Flags: uninsdeletevalue; Components: Player
Root: HKCR; Subkey: "AresProject"; ValueType: string; ValueName: ""; ValueData: "Ares Project"; Flags: uninsdeletekey; Components: Player
Root: HKCR; Subkey: "AresProject\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\Player_Editor\Ares.ico"; Components: Player
Root: HKCR; Subkey: "AresProject\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Player_Editor\Ares.Editor.exe"" ""%1"""; Check: ShallStartEditor
Root: HKCR; Subkey: "AresProject\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Player_Editor\Ares.Player.exe"" ""%1"""; Check: ShallStartPlayer
Root: HKCR; Subkey: ".apkg"; ValueType: string; ValueName: ""; ValueData: "AresPackedProject"; Flags: uninsdeletevalue; Components: Player
Root: HKCR; Subkey: "AresPackedProject"; ValueType: string; ValueName: ""; ValueData: "Ares Packed Project"; Flags: uninsdeletekey; Components: Player
Root: HKCR; Subkey: "AresPackedProject\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\Player_Editor\Ares.ico"; Components: Player
Root: HKCR; Subkey: "AresPackedProject\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Player_Editor\Ares.Editor.exe"" ""%1"""; Check: ShallStartEditor
Root: HKCR; Subkey: "AresPackedProject\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Player_Editor\Ares.Player.exe"" ""%1"""; Check: ShallStartPlayer

[UninstallRun]
Filename: "{app}\Player_Editor\Ares.WinSecurity.exe"; Parameters: "RemovePort"; WorkingDir: "{app}\Player_Editor\"; Flags: runascurrentuser; Components: Player

[Code]
var 
  MySetupType : String;

function ShallStartEditor() : Boolean;
begin
  MySetupType := WizardSetupType(False);
  if (CompareText(MySetupType, 'All') = 0) then
    Result := True
  else if (CompareText(MySetupType, 'Player_and_Editor') = 0) then
    Result := True
  else
    Result := False;
end;

function ShallStartPlayer() : Boolean;
begin
  MySetupType := WizardSetupType(False);
  if (CompareText(MySetupType, 'Player_only') = 0) then
    Result := True
  else
    Result := False;
end;

function ShallStartController() : Boolean;
begin
  MySetupType := WizardSetupType(False);
  if (CompareText(MySetupType, 'Controller_only') = 0) then
    Result := True
  else
    Result := False;
end;

function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1'          .NET Framework 1.1
//    'v2.0'          .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//    'v4.5.1'        .NET Framework 4.5.1
//    'v4.5.2'        .NET Framework 4.5.2
//    'v4.6'          .NET Framework 4.6
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key, versionKey: string;
    install, release, serviceCount, versionRelease: cardinal;
    success: boolean;
begin
    versionKey := version;
    versionRelease := 0;

    // .NET 1.1 and 2.0 embed release number in version key
    if version = 'v1.1' then begin
        versionKey := 'v1.1.4322';
    end else if version = 'v2.0' then begin
        versionKey := 'v2.0.50727';
    end

    // .NET 4.5 and newer install as update to .NET 4.0 Full
    else if Pos('v4.', version) = 1 then begin
        versionKey := 'v4\Full';
        case version of
          'v4.5':   versionRelease := 378389;
          'v4.5.1': versionRelease := 378675; // or 378758 on Windows 8 and older
          'v4.5.2': versionRelease := 379893;
          'v4.6':   versionRelease := 393295; // or 393297 on Windows 8.1 and older
        end;
    end;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + versionKey;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0 and newer use value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 and newer use additional value Release
    if versionRelease > 0 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= versionRelease);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;

function HasJavaInstalled(check32bit: Boolean): Boolean;
var
 ErrorCode: Integer;
 JavaInstalled : Boolean;
 ResultMsg : Boolean;
 Versions: TArrayOfString;
 I: Integer;
 regRoot: Integer;
begin
 // Check which view of registry should be taken:
 regRoot := HKLM
 begin
  if IsWin64 and not check32bit then
  begin
   regRoot := HKLM64
  end;
 end;
 if (RegGetSubkeyNames(regRoot, 'SOFTWARE\JavaSoft\Java Runtime Environment', Versions)) or (RegGetSubkeyNames(regRoot, 'SOFTWARE\JavaSoft\Java Development Kit', Versions)) then
 begin
  for I := 0 to GetArrayLength(Versions)-1 do
   if JavaInstalled = true then
   begin
    //do nothing
   end else
   begin
    if ( Versions[I][2]='.' ) and ( ( StrToInt(Versions[I][1]) > 1 ) or ( ( StrToInt(Versions[I][1]) = 1 ) and ( StrToInt(Versions[I][3]) >= 6 ) ) ) then
    begin
     JavaInstalled := true;
    end else
    begin
     JavaInstalled := false;
    end;
   end;
 end else
 begin
  if IsWin64 and not check32bit then
  begin
    JavaInstalled := HasJavaInstalled(True)
  end else
  begin 
    JavaInstalled := false;
  end;
 end;
 Result := JavaInstalled;
end;

function PrepareToInstall(var NeedsRestart: Boolean): String;
var 
  allOK : Boolean;
begin
  Result := '';
  allOK := True;
  MySetupType := WizardSetupType(False);
  if (IsComponentSelected('Editor')) then
  begin
    if not IsDotNetDetected('v4.5', 0) then
    begin
      Result := CustomMessage('needsNet45');
      allOK := False;
    end;
  end;    
  if (allOK and IsComponentSelected('Player')) then
  begin
    if not IsDotNetDetected('v4\Full', 0) then
    begin
      Result := CustomMessage('needsNet4');
      allOK := False;
    end;
  end;  
  if (allOK and IsComponentSelected('Controller')) then
  begin
    if not HasJavaInstalled(False) then
    begin
      Result := CustomMessage('needsJava')
    end;
  end;
end;

const
  NET_FW_SCOPE_ALL = 0;
  NET_FW_IP_VERSION_ANY = 2;

procedure SetFirewallException(AppName,FileName:string);
var
  FirewallObject: Variant;
  FirewallManager: Variant;
  FirewallProfile: Variant;
begin
  try
    FirewallObject := CreateOleObject('HNetCfg.FwAuthorizedApplication');
    FirewallObject.ProcessImageFileName := FileName;
    FirewallObject.Name := AppName;
    FirewallObject.Scope := NET_FW_SCOPE_ALL;
    FirewallObject.IpVersion := NET_FW_IP_VERSION_ANY;
    FirewallObject.Enabled := True;
    FirewallManager := CreateOleObject('HNetCfg.FwMgr');
    FirewallProfile := FirewallManager.LocalPolicy.CurrentProfile;
    FirewallProfile.AuthorizedApplications.Add(FirewallObject);
  except
  end;
end;

procedure RemoveFirewallException( FileName:string );
var
  FirewallManager: Variant;
  FirewallProfile: Variant;
begin
  try
    FirewallManager := CreateOleObject('HNetCfg.FwMgr');
    FirewallProfile := FirewallManager.LocalPolicy.CurrentProfile;
    FireWallProfile.AuthorizedApplications.Remove(FileName);
  except
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if ((CurStep=ssPostInstall) and IsComponentSelected('Player')) then
  begin
     SetFirewallException('Ares Player', ExpandConstant('{app}\')+'Player_Editor\Ares.Player.exe');
     SetFirewallException('Ares Player', ExpandConstant('{app}\')+'Player_Editor\Ares.CmdLinePlayer.exe');
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep=usPostUninstall then
  begin
     RemoveFirewallException(ExpandConstant('{app}\')+'Player_Editor\Ares.Player.exe');
     RemoveFirewallException(ExpandConstant('{app}\')+'Player_Editor\Ares.CmdLinePlayer.exe');
  end;
end;
